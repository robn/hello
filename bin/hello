#!/usr/bin/env perl

use 5.020;
use warnings;
use strict;
use experimental qw(postderef);

use IO::Async::Loop;
use TOML qw(from_toml);
use Module::Load qw(load);
use Future::Utils qw(try_repeat);
use Hello::Logger '$Logger';

my ($config, $err) = from_toml(do { local (@ARGV, $/) = ('config.toml'); <> });
warn $err unless $config;

#use Data::Dumper;
#print Dumper $config;

my $default_config = delete $config->{tester}->{_defaults_} // {};
my $default_interval = delete $default_config->{interval} // 120;
my $default_timeout  = delete $default_config->{timeout}  // 30;

my $loop = IO::Async::Loop->new;

my @collectors;

for my $collector_package (keys $config->{collector}->%*) {
  my $collector_config = delete $config->{collector}->{$collector_package} // {};

  load "Hello::Collector::$collector_package";

  my $collector = "Hello::Collector::$collector_package"->new(
    loop => $loop,
    %$collector_config,
  );

  push @collectors, $collector;
}

my @testers;

for my $tester_package (keys $config->{tester}->%*) {
  my $tester_list = delete $config->{tester}->{$tester_package} // [];

  load "Hello::Tester::$tester_package";

  for my $tester_config ($tester_list->@*) {
    my $tester_interval = delete $tester_config->{interval} // $default_interval;
    my $tester_timeout  = delete $tester_config->{timeout}  // $default_timeout;

    my $tester = "Hello::Tester::$tester_package"->new(
      loop     => $loop,
      interval => $tester_interval,
      timeout  => $tester_timeout,
      %$tester_config,
    );

    push @testers, $tester;
  }
}

Future->wait_all(
  map {
    my $tester = $_;
    try_repeat {
      my $Logger = $Logger->proxy({ proxy_prefix => $tester->name.': ' });

      $Logger->log("starting");

      $tester->test_result
        ->then(sub {
          my ($result) = @_;

          $Logger->log(["result: %s (%s) [%.2fs]", $result->state, $result->reason, $result->elapsed]);

          $_->collect($result) for @collectors;

          my $wait_time = int(.5 + $tester->interval - $result->elapsed);

          if ($wait_time < 0) {
            my $now = time;
            my $interval = $tester->interval;

            my $next_time = $result->start;
            my $skipped = 0;
            while ($next_time < $now) {
              $next_time += $interval;
              $skipped++;
            }

            $wait_time = $next_time - $now;

            $Logger->log("WARNING: last run took longer than interval $interval; skipped $skipped tests");
          }

          $Logger->log(["next run in %ds, at %s", $wait_time, scalar localtime(time() + $wait_time)]);

          $loop->delay_future(after => $wait_time);
        })
    } while => sub { 1 };
  } @testers
)->get;

#my @f = map { [$_->get] } Future->wait_all(map { $_->test } @testers)->get;
#use Data::Dumper;
#print Dumper \@f;
